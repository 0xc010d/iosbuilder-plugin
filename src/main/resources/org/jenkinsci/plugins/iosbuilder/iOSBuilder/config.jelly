<j:jelly xmlns:j="jelly:core" xmlns:st="jelly:stapler" xmlns:d="jelly:define" xmlns:l="/lib/layout" xmlns:t="/lib/hudson" xmlns:f="/lib/form">
    <f:entry title="Perform `pod install`" field="installPods">
        <f:checkbox />
    </f:entry>
    <f:entry title=".xcworkspace path" field="xcworkspacePath">
        <f:textbox />
    </f:entry>
    <f:entry title=".xcodeproj path" field="xcodeprojPath">
        <f:textbox />
    </f:entry>
    <f:entry title="Target" field="target">
        <f:textbox />
    </f:entry>
    <f:entry title="Scheme" field="scheme">
        <f:textbox />
    </f:entry>
    <f:entry title="Configuration" field="configuration">
        <f:textbox />
    </f:entry>
    <f:entry title="SDK" field="sdk">
        <f:select />
    </f:entry>
    <f:entry title="Additional xcodebuild parameters" field="additionalParameters">
        <f:textbox />
    </f:entry>
    <f:optionalBlock field="codeSign" title="Sign the binary">
        <f:entry title=".p12 key file" field="pkcs12ArchiveData">
            <j:invokeStatic className="org.jenkinsci.plugins.iosbuilder.signing.PKCS12ArchiveFactory" method="newInstance" var="pkcs12Archive">
                <j:arg type="java.lang.String" value="${instance.pkcs12ArchiveData}" />
                <j:arg type="java.lang.String" value="${instance.pkcs12ArchivePassword}" />
            </j:invokeStatic>
            <j:if test="${pkcs12Archive != null}">
                Current archive:
                <dl>
                    <j:forEach items="${pkcs12Archive.content}" var="item">
                        <dt>Private key</dt>
                        <dd>
                            ${item.getKey().getAlias()}
                            <j:if test="${item.getValue().commonName != null}">
                                <dl>
                                    <dt>Certificate</dt>
                                    <dd>${item.getValue().commonName}</dd>
                                </dl>
                            </j:if>
                        </dd>
                    </j:forEach>
                </dl>
                Select another:<br />
            </j:if>
            <input type="hidden" name="pkcs12ArchiveData" value="${instance.pkcs12ArchiveData}" />
            <input type="file" name="pkcs12ArchiveFile" jsonAware="yes" />
        </f:entry>
        <f:entry title=".p12 key password" field="pkcs12ArchivePassword">
            <f:password />
        </f:entry>
        <f:entry title=".mobileprovision file" field="mobileprovisionData">
            <j:invokeStatic className="org.jenkinsci.plugins.iosbuilder.signing.MobileprovisionFactory" method="newInstance" var="mobileprovision">
                <j:arg type="java.lang.String" value="${instance.mobileprovisionData}" />
            </j:invokeStatic>
            <j:if test="${mobileprovision != null}">
                Current profile:<br />
                <dl>
                    <dt>Mobileprovision</dt>
                    <dd>${mobileprovision.name}</dd>
                    <dt>Certificates</dt>
                    <dd>
                        <ul>
                            <j:forEach items="${mobileprovision.certificates}" var="certificate">
                                <li>${certificate.commonName}</li>
                            </j:forEach>
                        </ul>
                    </dd>
                </dl>
            </j:if>
            <input type="hidden" name="mobileprovisionData" value="${instance.mobileprovisionData}" />
            <input type="file" name="mobileprovisionFile" jsonAware="yes" />
        </f:entry>
        <f:entry title="Build .ipa" field="buildIPA">
            <f:checkbox />
        </f:entry>
    </f:optionalBlock>
</j:jelly>
